apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'checkstyle'

group = "io.github.kklaeger"
version = "1.2.0-SNAPSHOT"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    implementation("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")

    implementation("commons-io:commons-io:${commonsIOVersion}")

    implementation("org.apache.logging.log4j:log4j-core:${log4jVersion}")
    implementation("org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}")

    implementation("io.github.java-diff-utils:java-diff-utils:${javaDiffUtilsVersion}")

    implementation("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}")
    implementation("com.flipkart.zjsonpatch:zjsonpatch:${zjsonpatchVersion}")
    implementation("com.jayway.jsonpath:json-path:${jsonPathVersion}")
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'jApproval core'
                description = 'The core part of jApproval that provides the basic testing functionality.'
                url = 'https://github.com/kklaeger/japproval'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Konstantin Kl√§ger'
                        email = 'konstantin.klaeger@novatec-gmbh.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/kklaeger/japproval.git'
                    developerConnection = 'scm:git:ssh://github.com/kklaeger/japproval.git'
                    url = 'https://github.com/kklaeger/japproval'
                }
            }
            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    useGpgCmd()
    sign(publishing.publications)
}

tasks.withType(Sign) {
    onlyIf {
        project.hasProperty('signing.gnupg.keyName')
    }
}

checkstyle {
    configFile file("qa/checkstyle/checkstyle.xml")
    toolVersion = checkstyleVersion
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
